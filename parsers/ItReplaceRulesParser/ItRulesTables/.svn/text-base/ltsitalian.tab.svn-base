// Italian letter to sound rules. Note: a second transformation is needed
// which is defined in another file.
// 
set RBOUND = ["#", " ", "."];    // special boundary operator: use as RBOUND
set LBOUND = ["#", " "];    // special boundary operator: use as LBOUND
set Aset = ["a", "à", "á"];
set Eset = ["e", "é", "è"];
set Oset = ["o", "ò", "ó"];
set Iset = ["i", "ì", "ï"];
set Uset = ["u" , "ù", "ú"];
set strvow = [Aset, Eset, Oset];
set weakvow = [Iset, Uset];
set lvow = [Iset, Eset];
set ovow = [Aset, Oset, Uset];
set vow = [lvow,ovow];
set vplos = ["b", "d", "g"]; 
set uvplos = ["p", "t", "k"];
set liq = ["l", "r"];
set nas = ["m", "n"];
set sono = [vplos, liq, nas, "v"];

set plos = [vplos,uvplos];
set cons = [plos, "f", "s", "c", "z"];
set allcons = [cons, sono];

set ialist=["andr", "card", "cefal", "clas", "clast", "cor", "crom", "cron",
"dattil", "derm", "ectom", "ern", "fag", "fas", "fil", "fon", "gam", "gen",
"gloss", "gnos", "gonf", "latr", "figl", "iz", "lal", "latr", "mach", "megal",
"mel", "mer", "miel", "nom", "odin", "onim", "op", "ops", "pen", "pess",
"plas", "pleg", "scop", "sem", "soff", "som", "stom", "tecn", "tip", "trop",
"gr", "graf", "ac", "as"];
// (Daniele's list for ia becoming a hiatus after these,  see "ia" below)

# use NIL for empty right side in a replacement rule

"j" > "j" / LBOUND _ .; 
"j" > "j"; 

"più"  > "pju" / LBOUND _ RBOUND;

"dà" > "dA"  / LBOUND _ RBOUND;
"da" > "da"  / LBOUND _ RBOUND;
"è"  > "E"   / LBOUND _ RBOUND;
"e"  > "e"   / LBOUND _ RBOUND;
"là" > "lA"   / LBOUND _ RBOUND;
"la" > "la"   / LBOUND _ RBOUND;
"né" > "nE"   / LBOUND _ RBOUND;
"ne" > "ne"   / LBOUND _ RBOUND;
"sé" > "sE"   / LBOUND _ RBOUND;
"se" > "se"   / LBOUND _ RBOUND;
"sì" > "sI"   / LBOUND _ RBOUND;
"si" > "si"   / LBOUND _ RBOUND;

"bb" > "Bb"; 
"b" > "bi"/ LBOUND _ RBOUND;
"b" > "B" / ._ plos;
"b" > "b"; 


"c" > "ci" / LBOUND _ RBOUND;
"cuo" > "kwc" ;
"c'è"  > "7E" / LBOUND _ RBOUND;
"c'e"  > "7e" / LBOUND _ RBOUND;
"c'i"  > "7i" / LBOUND _ RBOUND;
"cch"  > "Kk" / vow _ vow;
"cci"  > "T7" / vow _ ovow;    // Sure about this?
"cci"  > "77i" /. _ RBOUND;  
"cci"  > "77" /. _ vow;
"chi"  > "kj"  / . _ vow;
"cc"   > "77" / . _ lvow;
"cc"   > "Kk" / . _ ovow;
"ck"   > "Kk";
"ch"   > "k"  / . _ vow;
"ci"    > "7"  / . _ ovow;
"c+"   > "7";
"c"    > "7"  / . _ lvow;
"c"  > "k";

"d" > "di" / LBOUND _ RBOUND;
"d"  > "D" /. _ plos;
"d"  > "d";

"f" > "Effe" / LBOUND _ RBOUND;
"ph" > "f" /. _ vow;
"f"  > "f";

"g" > "8i" / LBOUND _ RBOUND;
"gl+" > "L" / LBOUND _ RBOUND;
"gli" > "Li" / LBOUND _ RBOUND ;
"glie" > "Le" / LBOUND _ nas;
"glie" > "Le" / LBOUND _ liq;
"gli" > "gli" / LBOUND _ cons;
"gli" > "LL" / vow _ vow;
"gli" > "LLi" / vow _ allcons;
"ghi" > "gj" / . _ ovow;
"ggi" > "d8" / vow _ strvow;
"giu" > "8u" / . _ . ;
"gi" > "8" / . _ strvow;
"gh" > "g"  /  . _ . ;
"gg" > "d8" /  vow _ lvow;
"gg" > "Gg" /  vow _ ovow;
"gn" > "HH"  /  vow _ vow;
"gn" > "H"  /  . _ vow;
"gui" > "gwi" / . _ allcons;
"g" > "8" /  . _ lvow; 
"g" > "g" / ._.;

"h" > "akka" / LBOUND _ RBOUND;
"h" > "?"  / sono _ vow;
"h" > "?"  / vow _ vow;
"h" > NIL / ._. ;

"j" > "ilungo" / LBOUND _ RBOUND;
"j" > "j" ;

"kk" > "Kk";
"k" > "kappa" / LBOUND _ RBOUND;
"k" > "k" ;

"l" > "Elle" / LBOUND _ RBOUND;
"l+"> "l";
"l" > "l";

"m" > "Emme" / LBOUND _ RBOUND;
"m" > "m";

"ngli" > "QLi";
"ngl" > "Qgl" / vow _ vow;
"ngi" > "n8" / vow _ strvow;
// "ng" > "QQ" / vow _ lvow;
"ng" > "Qg" / vow _ ovow;
"ng" > "n8" / vow _ lvow;
"n" > "Q" / vow _ "qu";
"n" > "Enne" / LBOUND _ RBOUND;
"n"> "n";
"n+" > "n";

"pp" > "Pp";
"p" > "pi" / LBOUND _ RBOUND;
"p" > "p";

"q" > "ku" / LBOUND _ RBOUND;
"qui" > "kWj" / . _ vow;
"qu" > "kw" /. _ vow;
"q"  > "k";

"ria" > "RIa" / vow _ RBOUND;
"r" > "Erre" / LBOUND _ RBOUND;
"r" > "R" /  vow _ vow;
"r" > "R" /  LBOUND _ vow;
"rr" > "RR" /  vow _ vow;
"r" > "r";

"s" > "Esse" / LBOUND _ RBOUND;
"schi" > "ski"/  . _  "i" | cons;
"schi" > "skj"/  . _  vow;
"sch"  > "S" /  . _  ovow;
"sch"  > "sk" /  . _  vow;
"scia" > "Sa" / LBOUND _ allcons | RBOUND;
"scio" > "So" / LBOUND _ allcons | RBOUND;
"sciu" > "Su" / LBOUND _ allcons | RBOUND;
"scie" > "Se" / LBOUND _ allcons | RBOUND;
"scia" > "SSa" / vow _ allcons | RBOUND;
"scio" > "SSo" / vow _ allcons | RBOUND;
"sciu" > "SSu" / vow _ allcons | RBOUND;
"scie" > "SSe" / vow _ allcons | RBOUND;
"sci"  > "Sj" /  . _  Uset;
"sce"  > "Se" /  . _  cons | RBOUND;
"sci"  > "Si" /  . _  cons | RBOUND;
"sci"  > "Sj"  /  . _  vow;
"sci"  > "Si"  /  . _  .;
"sia" > "zIa" / vow _ RBOUND;
"sc"   > "sk" /  . _  ovow;
"sc"   > "S"  /  . _ lvow;
"s"    > "s"  / ovow _ lvow + RBOUND;
//"s"    > "z"  / ovow _ lvow;
"s"    > "z"  / . _ sono;
"s"    > "Esse" / LBOUND _ RBOUND;
"s"    > "s" ;

"t" > "ti" / LBOUND _ RBOUND;
"tt" > "Tt" ;
"t" > "t" ;

"via" > "vIa";
"vie" > "vIE";
"v" > "vu" / LBOUND _ RBOUND;
"v" > "v" ;

"w" > "doppja vu" / LBOUND _ RBOUND;
"w" > "v" ;

"x" > "iks" / LBOUND _ RBOUND;
"x" > "ks";

"y" > "ipsilon" / LBOUND _ RBOUND;
"y" > "i";

"zia" > "9ia" / vow _ RBOUND;
"zz" > "00" /  vow _ "are" + RBOUND;
"zz" > "00" /  vow _ "az" + vow;
"zz" > "00" /  vow _ "at" + vow;
"zz" > "T9" ;
"z" > "9Eta" / LBOUND _ RBOUND;
"z" > "0" / "n"_ vow;
"z" > "0" / LBOUND _ ovow | sono;
"z" > "9" ;

"anti" > "anti";
"aissim" > "aIssim" /. _ vow + RBOUND;
"aiu" > "Aju" / . _ cons;
"ae" > "Ae" / cons _ cons;
"aa" > "Aa" / cons _ RBOUND;
"ao" > "Ao" / cons _ cons;
"au" > "aU" / cons _ liq + vow;  // paura
"ai" > "AI" / cons + cons _ RBOUND;
"ai" > "Ai" / . _ RBOUND;
"ai" > "aI" / LBOUND _ cons;
"a" >  "a" ;
"à" >  "A" ;       // stressed 
"á" >  "a" ;

"èi" > "EI" / . _ RBOUND;
"ei" > "Ei" / liq _ RBOUND;
"ei" > "Ei" / allcons _ RBOUND;
"eo" > "Ec" / . _ RBOUND;
"eu" > "Eu" / LBOUND | cons _ cons;
"ea" > "EA" / .  _ cons;
"e" > "e" ;
"è" > "e" ;
"é" > "E" ;

"ua" > "Ua" / LBOUND + allcons _ RBOUND;
"uo" > "Uo" / LBOUND + allcons _ RBOUND;
"ue" > "UE" / LBOUND + allcons _ RBOUND;

"uo" > "uc" / cons _ RBOUND;
"uo" > "wc" / cons _ cons | liq | nas;
"uo" > "wo";
"uó" > "uc";
"ua" > "wa" / vplos _  . ;

"oissim" > "oIssim" /. _ vow + RBOUND;
"oi" > "ci" / . _ cons | RBOUND;
"oia"  > "cja" / . _ cons | RBOUND;
"oo" > "OO" / cons _ cons;
"oe" > "ce";
"o" > "c" / . _ "nn" + vow;
"o" > "o" ;
"ó" > "c" ;
"ò" > "c" ;

"u" > "uu" / LBOUND _ RBOUND;
"u" > "w" /  strvow _ strvow;
"u" > "u" ;
"ù" > "U" ;
"ú" > "u" ;

"ia" > "Ia" / LBOUND + allcons _ RBOUND;
"io" > "Io" / LBOUND + allcons _ RBOUND;
"ie" > "IE" / LBOUND + allcons _ RBOUND;
"ii" > "iI" / . _ RBOUND;
"ia" > "Ia" / ialist _ RBOUND;    // Daniele's list
"ia" > "ja" / plos _ cons + cons + vow;
"ia" > "ja" / sono + sono _ sono ;
"ia" > "Ia" / cons _ sono + vow;
//  "ia" > "Ia" / vow + cons + sono _ allcons | RBOUND;   // Daniele's list generalized.
"ia" > "ja" / vow _ .;
"ie" > "iE" / cons _ cons + vow;
"ie" > "jE" / vow _ .;
"io" > "io" / cons + sono _ cons | RBOUND;
"io" > "io" / cons + cons _ allcons | RBOUND;
"io" > "jo" / vow + cons _ allcons | RBOUND; 
"iu" > "ju" / cons _ . ;
"i" > "j"  / strvow _ strvow;
"i" > "i"  /. _ ovow;
"i" > "i" ;
"ì" > "I" ;
"í" > "i" ;
"ï" > "I" ;
" " > " ";
"." > "#";
"#" > "#";

